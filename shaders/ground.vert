#version 450

#extension GL_GOOGLE_include_directive : require
#extension GL_EXT_buffer_reference : require

#include "input_structures.glsl"

layout (location = 0) out vec3 outNormal;
layout (location = 1) out vec3 outColor;
layout (location = 2) out vec2 outUV;
layout (location = 3) out vec3 outWorldPos;
layout (location = 4) out vec4 outShadowCoord;

struct Vertex {
	vec3 position;
	float uv_x;
	vec3 normal;
	float uv_y;
	vec4 color;
};

layout(buffer_reference, std430) readonly buffer VertexBuffer{
	Vertex vertices[];
};

// Declaring only the prefix we read; the pipeline layout's 80-byte range is
// a superset and validation is fine with a shader that uses less.
layout( push_constant ) uniform constants
{
	mat4 render_matrix;
	VertexBuffer vertexBuffer;
} PushConstants;

void main()
{
	Vertex v = PushConstants.vertexBuffer.vertices[gl_VertexIndex];
	vec4 worldPos = PushConstants.render_matrix * vec4(v.position, 1.0);
	gl_Position = sceneData.viewproj * worldPos;

	outNormal = normalize(mat3(PushConstants.render_matrix) * v.normal);
	outColor = v.color.xyz * materialData.colorFactors.xyz;
	outUV.x = v.uv_x;
	outUV.y = v.uv_y;
	outWorldPos = worldPos.xyz;
	outShadowCoord = sceneData.lightViewProj * worldPos;
}
